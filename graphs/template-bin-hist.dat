reset

#number of intervals, could be like frames/20?
n=200 

#minmaxing
stats "tmp/INFILE1" u 2
min=STATS_lo_quartile
max=STATS_up_quartile
stats "tmp/INFILE2" u 2
if (min > STATS_lo_quartile) min=STATS_lo_quartile
if (max < STATS_up_quartile) max=STATS_up_quartile
min=(min-.2)
max=(max+.2)

width=(max-min)/n #interval width
ofs=width/4
#function used to map a value to the intervals
hist(x,width)=width*floor(x/width)+width/2.0
set term png #output terminal and file
set output "graphs/graph-FILENAME.png"
set xrange [min:max]
set yrange [0:]
#to put an empty boundary around the
#data inside an autoscaled graph.
set offset graph 0.05,0.05,0.05,0.0
set xtics min,(max-min)/5,max
set boxwidth width*0.2 #relative
set style fill solid 0.7 #fillstyle
set tics out nomirror
set xlabel "frametime in ms"
set ylabel "Occurrences sampled over TOTAL frames"
#count and plot
# file using hist(2nd col, width)

plot "tmp/INFILE1" u (hist($2,width)-ofs) smooth freq w boxes lc rgb"red" title "Vulkan", \
    "tmp/INFILE2" u (hist($2,width)+ofs) smooth freq w boxes lc rgb"blue" title "OpenGL"

set terminal pop
set output
replot
