reset
n=200 #number of intervals
min=2.3 #min value
max=2.8 #max value
width=(max-min)/n #interval width
ofs=width/4
#function used to map a value to the intervals
hist(x,width)=width*floor(x/width)+width/2.0
set term png #output terminal and file
set output "histogram.png"
set xrange [min:max]
set yrange [0:]
#to put an empty boundary around the
#data inside an autoscaled graph.
set offset graph 0.05,0.05,0.05,0.0
set xtics min,(max-min)/5,max
set boxwidth width*0.2 #relative
set style fill solid 0.7 #fillstyle
set tics out nomirror
set xlabel "frametime in ms"
set ylabel "Occurrences sampled over 5000 frames"
#count and plot
# file using hist(2nd col, width)

plot "tmp/vk-06-28-16-42-37.tmp" u (hist($2,width)-ofs) smooth freq w boxes lc rgb"red" title "Vulkan", \
    "tmp/gl-06-28-16-42-37.tmp" u (hist($2,width)+ofs) smooth freq w boxes lc rgb"blue" title "OpenGL"

set terminal pop
set output
replot